plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "checkstyle"
}

base {
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    compileJava.options.encoding = compileTestJava.options.encoding = javadoc.options.encoding = "UTF-8"

    archivesName = project.maven_name ?: rootProject.maven_name
    group = project.maven_group ?: rootProject.maven_group
    version = project.maven_version ?: rootProject.maven_version
}

configurations {
    include

    implementation.extendsFrom include
    api.extendsFrom include
}

repositories {
    mavenCentral()
    maven {
        name = "lenni0451"
        url = "https://maven.lenni0451.net/everything"
    }
}

dependencies {
    if (new File(projectDir, "libs").exists()) { //Include all jars in the libs folder
        include fileTree(include: ["*.jar"], dir: "libs")
    }

    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    compileOnly "org.jetbrains:annotations:26.0.2-1"
    compileOnly(annotationProcessor("org.projectlombok:lombok:1.18.42"))
    api "net.lenni0451.commons:asm:1.9.0"
    api "org.ow2.asm:asm:9.9.1"
    api "org.ow2.asm:asm-commons:9.9.1"
    api "org.ow2.asm:asm-tree:9.9.1"
    api "org.ow2.asm:asm-analysis:9.9.1"
}

java {
    withSourcesJar()
    withJavadocJar()
}

processResources {
    //Config file: "version: ${version}"
    inputs.properties(
            "version": project.maven_version
    )

    filesMatching("config.yml") {
        expand(
                "version": project.maven_version
        )
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

jar {
    dependsOn configurations.include
    from {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        configurations.include.collect {
            zipTree(it)
        }
    } {
        exclude "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
    }
}

publishing {
    repositories {
        maven {
            name = "reposilite"
            def releasesUrl = "https://maven.lenni0451.net/releases"
            def snapshotsUrl = "https://maven.lenni0451.net/snapshots"
            url = project.maven_version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
        maven {
            name = "ossrh"
            def releasesUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = project.maven_version.endsWith("SNAPSHOT") ? snapshotsUrl : releasesUrl

            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifactId = project.maven_name ?: rootProject.maven_name
            groupId = rootProject.maven_group
            version = rootProject.maven_version

            from components.java

            pom {
                name = rootProject.name
                description = rootProject.maven_description
                url = "https://github.com/" + rootProject.github_repo
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/" + rootProject.github_repo + "/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "Lenni0451"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/" + rootProject.github_repo + ".git"
                    developerConnection = "scm:git:ssh://github.com/" + rootProject.github_repo + ".git"
                    url = "github.com/" + rootProject.github_repo
                }
            }
        }
    }
}

checkstyle {
    toolVersion = "10.16.0" //Latest version for Java 8: 9.3
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
}

signing {
    setRequired false
    sign configurations.archives
    sign publishing.publications.maven
}

project.tasks.withType(PublishToMavenRepository).forEach {
    it.dependsOn(project.tasks.withType(Sign))
}
build.dependsOn(check)

task runObfTest(type: JavaExec) {
    group = 'verification'
    description = 'Run obfuscated JAR test'
    classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
    mainClass = 'test.ObfTestRunner'
}
